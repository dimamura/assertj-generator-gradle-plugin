buildscript {
    ext.kotlin_version = '1.3.72'
    ext.dokka_version = '0.10.0'

    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

plugins {
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.12.0"
    id 'org.jmailen.kotlinter' version '1.26.0'
}

group 'com.github.dimamura'
version '1.1.3'

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'

sourceCompatibility = 1.8

repositories {
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    implementation("org.assertj:assertj-assertions-generator:2.2.0") {
        exclude module: 'logback-classic'
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

dokka {
    outputFormat = "javadoc"
}

gradlePlugin {
    plugins {
        assertjGenerator {
            id = 'com.github.dimamura.assertj-generator'
            implementationClass = 'com.github.fhermansson.gradle.assertj.plugin.AssertjGeneratorPlugin'
        }
    }
}
pluginBundle {
    website = 'https://github.com/dimamura/assertj-generator-gradle-plugin'
    vcsUrl = 'https://github.com/dimamura/assertj-generator-gradle-plugin'
    description = 'Generate Assertj assertion classes. (forked from fhermansson/assertj-generator-gradle-plugin, update dependencies.)'
    tags = ['code-generation', 'assertj', 'java']

    plugins {
        assertjGenerator {
            id = 'com.github.dimamura.assertj-generator'
            displayName = 'Assertj Generator plugin'
        }
    }
}

afterEvaluate {
    javadoc.enabled = false
    publishPluginJavaDocsJar.from(dokka)
}


compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

